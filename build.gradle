buildscript {
    ext {
        springBootVersion = '1.5.0.RC1'
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id 'groovy'

    id 'checkstyle'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'pl.allegro.tech.build.axion-release' version '1.4.1'
}
apply plugin: 'org.springframework.boot'

scmVersion {
    tag.prefix = project.name

    hooks {
        pre 'fileUpdate',
                [file: 'docs/index.md', pattern: { v, c -> /avro-cli-$v/ }, replacement: { v, c -> "avro-cli-$v" }]
        pre 'commit'
    }
}

group = 'io.github.rkluszczynski'
project.version = scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

def versions = [
        avro      : '1.8.1',
        spock     : '1.0-groovy-2.4',

        checkstyle: '6.3',
        gradle    : '3.3'
]

dependencies {
    compile 'org.springframework.boot:spring-boot-starter',
            "org.apache.avro:avro:${versions.avro}",
            'com.beust:jcommander:1.58'

    testCompile 'com.github.tomakehurst:wiremock:2.5.0',
            'cglib:cglib-nodep:3.2.4',
            'org.objenesis:objenesis:2.5.1',
            "org.spockframework:spock-core:${versions.spock}",
            "org.spockframework:spock-spring:${versions.spock}",
            'org.springframework.boot:spring-boot-starter-test'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

configurations {
    checkstyleConfig {
        transitive = false
    }
}

dependencies {
    checkstyleConfig "com.puppycrawl.tools:checkstyle:${versions.checkstyle}"
}

checkstyle {
    toolVersion = "${versions.checkstyle}"
//    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}

task wrapper(type: Wrapper) {
    gradleVersion = "${versions.gradle}"
}
